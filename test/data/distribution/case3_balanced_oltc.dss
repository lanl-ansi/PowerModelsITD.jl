!
! This script is a modified version of the 3-bus system
! with a oltc (voltage regulator) 
! Juan Ospina, September 2021
!

!Clear
New Circuit.3Bus_example
!  define a really stiff source
~ basekv=230 pu=1.00 MVAsc3=200000 MVAsc1=210000

! Substation Transformer
New Transformer.SubXF Phases=3 Windings=2 Xhl=0.01
~ wdg=1 bus=sourcebus conn=wye kv=230   kva=25000    %r=0.0005
~ wdg=2 bus=Substation   conn=wye kv=13.8  kva=25000   %r=0.0005


! FEEDER 1-PHASE VOLTAGE REGULATORS
! Define low-impedance 2-wdg transformer

New Transformer.reg1a phases=1 Windings=2 bank=reg1 XHL=0.01 kVAs=[10666 10666]
~ Buses=[Substation.1 RG60.1] kVs=[7.97  7.97] %LoadLoss=0.01
~ %rs=[0 0] ! correct default here

New Transformer.reg1b phases=1 Windings=2 bank=reg1 XHL=0.01 kVAs=[10666 10666]
~ Buses=[Substation.2 RG60.2] kVs=[7.97  7.97] %LoadLoss=0.01
~ %rs=[0 0] ! correct default here

New Transformer.reg1c phases=1 Windings=2 bank=reg1 XHL=0.01 kVAs=[10666 10666]
~ Buses=[Substation.3 RG60.3] kVs=[7.97  7.97] %LoadLoss=0.01
~ %rs=[0 0] ! correct default here

! POINT REGULATOR CONTROLS TO REGULATOR TRANSFORMER AND SET PARAMETERS
new regcontrol.creg1a  transformer=reg1a winding=2 vreg=122  band=2  ptratio=65  ctprim=700  R=3   X=9 
new regcontrol.creg1b like=creg1a   transformer=reg1b 
new regcontrol.creg1c like=creg1a   transformer=reg1c 


!Define Linecodes

New linecode.556MCM nphases=3 basefreq=60  ! ohms per 5 mile
~ rmatrix = ( 0.1000 | 0.0400    0.1000 |  0.0400    0.0400    0.1000)
~ xmatrix = ( 0.0583 |  0.0233    0.0583 | 0.0233    0.0233    0.0583)
~ cmatrix = (50.92958178940651  | -0  50.92958178940651 | -0 -0 50.92958178940651  ) ! small capacitance


New linecode.4/0QUAD nphases=3 basefreq=60  ! ohms per 100ft
~ rmatrix = ( 0.1167 | 0.0467    0.1167 | 0.0467    0.0467    0.1167)
~ xmatrix = (0.0667  |  0.0267    0.0667  |  0.0267    0.0267    0.0667 )
~ cmatrix = (50.92958178940651  | -0  50.92958178940651 | -0 -0 50.92958178940651  )  ! small capacitance


!Define lines
New Line.OHLine    Phases=3 bus1=RG60.1.2.3   Bus2=Primary.1.2.3  linecode = 556MCM   length=1  ! 5 mile line
!New Line.OHLine  bus1=Substation.1.2.3  Primary.1.2.3  linecode = 556MCM   length=1  ! 5 mile line
New Line.Quad    Bus1=Primary.1.2.3  loadbus.1.2.3  linecode = 4/0QUAD  length=1   ! 100 ft


!Loads - single phase

New Load.L1 phases=1  loadbus.1.0   ( 13.8 3 sqrt / )   kW=3000   kvar=1500  model=1
New Load.L2 phases=1  loadbus.2.0   ( 13.8 3 sqrt / )   kW=3000   kvar=1500  model=1
New Load.L3 phases=1  loadbus.3.0   ( 13.8 3 sqrt / )   kW=3000   kvar=1500  model=1

!GENERATORS DEFINITIONS
New generator.gen1 Bus1=loadbus.1.2.3 Phases=3 kV=( 13.8 3 sqrt / )  kW=2000 pf=1 conn=wye Model=3

Set VoltageBases = "230,13.8"
Set tolerance=0.000001
set defaultbasefreq=60
Set MaxControlIter=20
!Calcvoltagebases

! Transformer OLTC control
Transformer.reg1a.Tap=(0.00625  16 * 1 +)   ! Tap
Transformer.reg1b.Tap=(0.00625  7 * 1 +)   ! Tap
Transformer.reg1c.Tap=(0.00625  7 * 1 +)   ! Tap

!Solve

!show taps